#!/usr/bin/env zsh
export DOTFILES_ROOT=~/.dotfiles
source "$DOTFILES_ROOT/shell/feature-detection.sh"

bindkey -e  # emacs keys

# History
HISTFILE=~/.zsh_history
HISTSIZE=3000
SAVEHIST=3000
setopt APPEND_HISTORY
setopt HIST_IGNORE_DUPS


# Auto-completion
if [ -n "$BREW_PREFIX" ] && [ -d "$BREW_PREFIX/share/zsh-completions" ]; then
    fpath=($BREW_PREFIX/share/zsh-completions $fpath)
fi
autoload -U compinit
zstyle ':completion:*' use-cache on
zstyle ':completion:*' cache-path ~/.cache/zsh
compinit -u

# CD recent
autoload -Uz chpwd_recent_dirs cdr add-zsh-hook
add-zsh-hook chpwd chpwd_recent_dirs
zstyle ':completion:*' menu selection

source "$DOTFILES_ROOT/shell/index.sh"

# Prompt
_get_git_branch() {
    local branch
    branch=$(git branch 2>/dev/null | while read line; do
        [ $line '=~' '\* .*' ] && echo ${line[3,${#line}]}
    done)
    echo ${branch:+$branch$1}
}

setopt PROMPT_SUBST
if [[ $(uname -m) == arm64 ]]; then
    _arch=" arm"
fi
prompt_color() {
    PS1="%F{$1}\$(_get_git_branch :)%c \$(date "+%H%M")$_arch %(?.$.%F{161}$%f) %f" # Non-breaking space
}
prompt_color $PROMPT_COLOR

# Put here since virtualenv will change PS1
which doenv > /dev/null 2>&1 && doenv

# Job list title
if [[ -n "$TMUX" ]]; then
    _base_title() {
        for p in $cdxpath; do
            if [[ "$PWD" == "$p" ]]; then
                echo "${PWD##*/}" # # is remove prefix, ## is greedy
                return
            elif [[ "$PWD" == "$p"* ]]; then
                local remaining=${PWD#$p/}
                if [[ "$remaining" == */* ]]; then
                    echo "${remaining%%/*}/…" # % is remove suffix
                else
                    echo "${remaining%%/*}"
                fi
                return
            fi
        done
        echo "…/${PWD##*/}"
    }
    precmd() {
        local mark=""
        [[ $#jobtexts != 0 ]] && mark=" ⊕ "
        printf "\ek$(_base_title)$mark${jobtexts/ */}\e\\"
    }
    preexec() {
        local mark=
        [[ $#jobtexts != 0 ]] && mark=" ˖ "
        local job=${1/ */}
        if [[ "$job" == "fg" ]]; then
            job=${jobtexts[%%]/ */}
        fi
        printf "\ek$(_base_title) ◦ $job$mark${jobtexts/ */}\e\\"
    }
fi

# Quickly vim
fg_vim() {
    if jobs %vim 2>/dev/null; then
        fg %vim 2>/dev/null
    elif jobs %view 2>/dev/null; then
        fg %view 2>/dev/null
    elif jobs %dot 2>/dev/null; then
        fg %dot 2>/dev/null
    else
        BUFFER="vim"
        zle accept-line
    fi
    precmd
    zle redisplay
}
zle -N fg_vim
bindkey '\Cz' fg_vim

if [[ -n "$TMUX" ]]; then
    xvimopen() {
        if jobs %vim 2>/dev/null; then
            tmux send-keys C-z C-f
        else
            _lazycomp 'vim' _xvimopen_impl
        fi
    }
fi

# Messages at a glance
_do_message() {
    local content char sym
    content=$(echo && "$@")
    echo "$content"
    read -k char
    sym=$(tput cuu1 el)
    echo -n "$(while IFS= read -r line; do echo -n "$sym"; done <<< "$content")"
    zle reset-prompt
    zle -U "$char"
}
_message_git_st() { _do_message git -c color.status=always st; }
zle -N _message_git_st
bindkey '\es' _message_git_st

_message_git_l_inner() { git -c color.status=always st | head -n 1 && git -c color.status=always l --color; }
_message_git_l() { _do_message _message_git_l_inner; }
zle -N _message_git_l
bindkey '\eg' _message_git_l


# Syntax highlighting
# Must be at the bottom
if [[ -n "$BREW_PREFIX" ]] && [[ -f $BREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
    source $BREW_PREFIX/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
elif [[ -f /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh ]]; then
    source /usr/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
fi

true # Avoid error exit status
